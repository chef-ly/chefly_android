buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'
android {

    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.se491.chef_ly"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }
    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        all*.exclude group: 'org.json', module: 'json'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->

                    def date = new Date();
                    def formattedDate = date.format('yyyMMddHHmmss')
                    output.outputFile = new File(output.outputFile.parent,
                            output.outputFile.name.replace("-release", "-" + formattedDate)
                    )
                }

            }
        }
        debug {
            testCoverageEnabled true
        }

    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'

    }

    lintOptions { warning 'InvalidPackage' }
    productFlavors {
        free {}
        paid {}
//        alpha {
//            applicationId 'com.se491.chef_ly.alpha'
//            buildConfigField "boolean", "free", "true"
//            minSdkVersion 18
//            targetSdkVersion 25
//            versionCode 2
//            versionName "2"
//        }
    }
    lintOptions {
        abortOnError false
    }
}

ant.importBuild 'assets.xml'
preBuild.dependsOn(list, checksum)
clean.dependsOn(clean_assets)

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile project(':pocketsphinx-android-5prealpha-release')

    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.auth0.android:auth0:1.7.0'
    compile 'com.auth0.android:lock:2.4.0'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.google.android.gms:play-services-appindexing:9.8.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'

    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.6.3'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
}

jacoco {
    toolVersion = '0.7.5.201505241946'
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true

}